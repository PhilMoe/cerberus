' Cerberus X - Draw Primitives example using a tringle fan

Strict

Import mojo2

Class MyApp Extends App

	Field myCanvas:Canvas
	Field grabbed:Bool
	Field material:Material
	Field verts:Float[]
	Field vList:FloatList
	Field texcs:Float[]
	Field img:Image
	Field isVisualisationEnabled:Bool = True
	
	Method OnCreate:Int()

		myCanvas = New Canvas()
		
		'material = Material.Load("graphics.png", Image.Filter,  Shader.DefaultShader())
		img = Image.Load("graphics.png", 0.5, 0.5, Image.RepeatST|Image.Filter|Image.Mipmap)
		material = img.Material
				
		vList = New FloatList()				         
		Return 0		
	End
	
		
	Function DrawVertexVisualisation:Void(myCanvas:Canvas, verts:Float[])
		myCanvas.SetLineWidth(3.0) ' Seems not to work on html5 Target.
		
		For Local i:Int=2 Until verts.Length() Step 2			
			myCanvas.SetColor(0,0.8,0)
			myCanvas.DrawLine(verts[i-2], verts[i-1],verts[i], verts[i+1])
			myCanvas.DrawLine(verts[0], verts[1],verts[i], verts[i+1])
			myCanvas.DrawCircle(verts[i], verts[i+1], 7.0)
		Next
		If verts.Length()>=2
			myCanvas.SetColor(0.8, 0, 0) 
			myCanvas.DrawCircle(verts[0], verts[1], 7.0)
		EndIf
	End
	
	
	Method OnRender:Int()
	
		myCanvas.Clear(.1, .1, .1)
				
		myCanvas.SetColor(0.8, 0, 0) 
		myCanvas.DrawText("Move the mouse", DeviceWidth()/2, DeviceHeight() - 26, 0.5)	
			
		myCanvas.SetColor(1.0, 1.0, 1.0)
		myCanvas.DrawTriangleFan(MouseX()*0.5, MouseY()*0.5, verts, img, MouseX(), MouseY(), 0.1, 0.1)

		If isVisualisationEnabled DrawVertexVisualisation(myCanvas, verts)

		myCanvas.Flush()
		Return 0
	End
	
	
	Method OnUpdate:Int()
		If MouseHit(0)
			vList.AddLast(MouseX())
			vList.AddLast(MouseY())
			verts = vList.ToArray()
		EndIf
		
		If KeyHit(KEY_SPACE)
			isVisualisationEnabled = Not isVisualisationEnabled
		EndIf
		
		If KeyHit(KEY_ESCAPE)
			verts = []
			vList.Clear()
		EndIf
		
		Return 0
	End

End

Function Main:Int()
	New MyApp()
	Return 0
End