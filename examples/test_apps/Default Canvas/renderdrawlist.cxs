' Cerberus X drawlist example
'
' A simple demo that shows how to draw all the items in a drawlist with a single command

Strict

Import mojo2

Class MyApp Extends App

	Field myCanvas:Canvas
	Field myCanvas2:Canvas
	Field myDrawList:DrawList
	Field myImage:Image
	
	
	Method OnCreate:Int()

		myCanvas = New Canvas()
		myCanvas2 = New Canvas()
		myDrawList = New DrawList()
		myImage = Image.Load("Logo.png")
		
		' draw some circles into the list, rather than directly onto the canvas
	
		For Local i:Int = 0 Until 360 Step 5
			myDrawList.SetAlpha(Rnd(0.1, 0.9))
			myDrawList.SetColor(Rnd(), Rnd(), Rnd())
			Local radius:Float = Rnd(40, 300)
			Local cx:Float = radius * Cos(i)
			Local cy:Float = radius * Sin(i)
			myDrawList.DrawCircle(cx, cy, radius / 10)		
		Next
		myDrawList.SetAlpha(0.5)
		myDrawList.SetColor(1, 1, 1)
		myDrawList.DrawImage(myImage, 0, 0)			

		SetUpdateRate(1)
		Return 0
	End
	
	
	Method OnRender:Int()
		
		myCanvas.Clear(0, 0, 0.25)
		myCanvas2.Clear(0, 0.5, 0)
		
		' draw everything in the list in one pass
		' translate to the mouse position and rotate continuously
		myCanvas.RenderDrawList(myDrawList, MouseX(), MouseY(), Millisecs() * 0.01)
		' do the same again, but rotate in the opposite direction
		myCanvas2.RenderDrawList(myDrawList, MouseX(), MouseY(), Millisecs() * -0.01)
		myCanvas.Flush()
		myCanvas2.Flush()
		Return 0
	End

End


Function Main:Int()
	New MyApp()
	Return 0
End
