Strict
#GLFW_WINDOW_TITLE="2D in 3D Example"
#GLFW_WINDOW_WIDTH=800					'Set this and HEIGHT to 0 for a 'windowless' glfw app
#GLFW_WINDOW_HEIGHT=600	
'#GLFW_WINDOW_RESIZABLE=False
'#GLFW_WINDOW_FULLSCREEN=True

#OPENGL_DEPTH_BUFFER_ENABLED=True
#OPENGL_GLES20_ENABLED=True

Import mojo2
Import math3d
Import opengl.gles20

#If TARGET="glfw"
Import brl.process
#End

Class Canvas3D Extends Canvas
	
	Method SetCamLookAt:Void(camx:Float, camy:Float, camz:Float, eyex:Float, eyey:Float, eyez:Float, upVector:Float[])
		Local viewMatrix := [1.0,0.0,0.0,0.0, 0.0,1.0,0.0,0.0, 0.0,0.0,1.0,0.0, 0.0,0.0,0.0,1.0]
		Mat4LookAtLH(camx, camy, camz,     eyex, eyey, eyez,     upVector[0], upVector[1], upVector[2], viewMatrix)
		Self.SetViewMatrix(viewMatrix)	
	End
	
	Method SetProjection3D:Void(fov:Float, aspectratio:Float, near:Float, far:Float)
		Local projMatrix := [1.0,0.0,0.0,0.0, 0.0,1.0,0.0,0.0, 0.0,0.0,1.0,0.0, 0.0,0.0,0.0,1.0]
    	Mat4PerspectiveLH(fov, aspectratio, near, far, projMatrix)		
        Self.SetProjectionMatrix(projMatrix)
    End
    
	Method SetModelMatrix3D:Void(xpos:Float, ypos:Float, zpos:Float, pitch:Float, yaw:Float, roll:Float, xscale:Float,yscale:Float,zscale:Float)  
		' Code inspired by the CreateMatrixFast Function from the minib3d module
		Local sx:Float, sy:Float, sz:Float, cx:Float, cy:Float, cz:Float, theta:Float
    	Local modelMatrix1 := [1.0,0.0,0.0,0.0, 0.0,1.0,0.0,0.0, 0.0,0.0,1.0,0.0, 0.0,0.0,0.0,1.0]
		
		'' rotation angle about X-axis (pitch) 
		sx = Sin(pitch)
		cx = Cos(pitch)
		
		'' rotation angle about Y-axis (yaw) 
		sy = Sin(yaw)
		cy = Cos(yaw)
		
		'' rotation angle about Z-axis (roll) 
		sz = Sin(roll)
		cz = Cos(roll)
		
		Local sycz:Float = sy*cz
		Local cysz:Float = cy*sz
		Local sysz:Float = sy*sz
		Local cycz:Float = cy*cz
		
		'' determine left axis
		modelMatrix1[0] = (cycz+sysz*sx) *xscale
		modelMatrix1[1] = (cx*sz) *xscale
		modelMatrix1[2] = (-sycz+cysz*sx) *xscale
		modelMatrix1[3] = 0.0
		
		'' determine up axis
		modelMatrix1[4] = (-cysz+sycz*sx) *yscale
		modelMatrix1[5] = (cx*cz) *yscale
		modelMatrix1[6] = (sysz+cycz*sx) *yscale
		modelMatrix1[7] = 0.0
		
		'' determine forward axis
		modelMatrix1[8] = (sy*cx) *zscale
		modelMatrix1[9] = (-sx) *zscale
		modelMatrix1[10] = (cx*cy) *zscale
		modelMatrix1[11] = 0.0
	
		modelMatrix1[12] = xpos
		modelMatrix1[13] = ypos
		modelMatrix1[14] = zpos
		modelMatrix1[15] = 1.0
		Self.SetModelMatrix(modelMatrix1)
	End
End

'*******************************************************************    
' Define the myClass that extends the App Class.    
Class myClass Extends App
	Field _cW:Int
	Field _cH:Int
	Field canv3D:Canvas3D
	Field canvUI:Canvas
	Field drwLstUI:DrawList
	Field drwLst3D:DrawList

	Field imgLogo:Image
	Field imgTiles:Image[]
	Field imgPlant:Image

	Field camx:Float = 0
	Field camy:Float = -80
	Field camz:Float = -140
	
	Field stime:Int = Millisecs()
	Field ltime:Int
	Field delta:Float
	Field etime:Int
	Field dtime:Int
	Field iFPS:Int
	Field FPS:Int = 60
	
	'---------------------------------------------------------------
    Method OnCreate:Int()
        SetUpdateRate(0)
        SetSwapInterval(0)
        
        _cW = DeviceWidth()
        _cH = DeviceHeight()
        imgLogo = Image.Load("logoCX.png",0.5,0.5)
        imgPlant = Image.Load("plant.png",0.5,1.0,0)
        imgTiles = Image.LoadFrames("tiles.png",4, False,0.5,0.5,0)

		canvUI = New Canvas
		drwLstUI = New DrawList
		
        canv3D = New Canvas3D
		drwLst3D = New DrawList
        
        canv3D.SetProjection3D(45 , DeviceWidth()/Float(DeviceHeight()), 0.1, 30000)
		canv3D.SetCamLookAt(camx, camy, camz,  0, 0, 0,  [0.0, 1.0, 0.0] )

        Return 0
    End
    
	'---------------------------------------------------------------
    Method OnUpdate:Int()
 		iFPS += 1
		etime = Millisecs()
		dtime = etime - stime
		If dtime >= 1000
			FPS = iFPS
			iFPS = 0
			stime = etime
		Endif
		delta = etime-ltime
		ltime = etime
		
		Local deltamod:Float = 4.0 * delta/60.0
		
    	If (KeyDown(KEY_LEFT) Or (TouchDown(0)=1 And TouchX(0) < _cW/2)) Then camx-= deltamod
    	If (KeyDown(KEY_RIGHT) Or (TouchDown(0)=1 And TouchX(0) >= _cW/2)) Then camx+= deltamod
    	If (KeyDown(KEY_UP) Or (TouchDown(0)=1 And TouchY(0) < _cH/2)) Then camz+= deltamod
    	If (KeyDown(KEY_DOWN) Or (TouchDown(0)=1 And TouchY(0) >= _cH/2)) Then camz-= deltamod
    	
    	If KeyDown(KEY_PAGEUP) Then camy+= deltamod
    	If KeyDown(KEY_PAGEDOWN) Then camy-= deltamod
    	If KeyDown(KEY_SPACE)
    		camx = 0.0
    		camy = -80.0
			camz = -140.0
		Endif
#If TARGET="glfw"
    	If KeyDown(KEY_ESCAPE) Then ExitApp(0)
#End   	
		canv3D.SetCamLookAt(camx, camy, camz,     camx, 0, camz+140.0, [0.0, 1.0, 0.0])
		
		
        Return 0
    End
    
	'---------------------------------------------------------------
    Method OnRender:Int()

		glEnable(GL_DEPTH_TEST)
		glDepthFunc(GL_LEQUAL)
		glClear(GL_DEPTH_BUFFER_BIT)
		
'		glClear(GL_COLOR_BUFFER_BIT)
		
		canvUI.Clear()
		drwLstUI.Reset()
		drwLst3D.Reset()
        
        canv3D.Clear(0 ,0,.5)	
		canv3D.PushMatrix()
		canv3D.SetColor(1,1,1)        
        Local items:Int
        Local items2:Int
        Local size:Int=1024
        'Ground
        Seed=0
		For Local z:Int = size To -size Step -16
			For Local x:Int = -size To size Step 16
	        	drwLst3D.DrawImage(imgTiles[Int(Rnd(0,4))],x,z)
	        	items+=1
	        Next
        Next
        canv3D.SetModelMatrix3D(0,0,0, 90.0,0.0,0.0, 1.0,1.0,1.0)
        canv3D.RenderDrawList(drwLst3D)

        'Plants
        Seed=1563
		Local mm:=[1.0,0.0,0.0,0.0, 0.0,1.0,0.0,0.0, 0.0,0.0,1.0,0.0, 0.0,0.0,0.0,1.0]
		For Local z:Int = size To -size Step -16
			canv3D.SetModelMatrix3D(0,0,z+8, 0.0,0.0,0.0, 1.0,1.0,1.0)
			For Local x:Int = -size To size Step 16
	        	If Int(Rnd(1,100)) >= 90
		        	canv3D.DrawImage(imgPlant,x,0.0) 
		        	items2+=1
				Endif
	        Next
        Next
'        Local in:Float[2]
'        Local out:Float[3]
'        in[0] = MouseX()
'        in[1] = MouseY()
'        canv3D.TransformCoords([MouseX(),MouseY()],out)
        canv3D.PopMatrix()

		' Draw  UI
        drwLstUI.SetColor(1,1,1)
        drwLstUI.DrawText( "Camera position = "+Int(camx)+":"+Int(camy)+":"+Int(camz), 10,10, 0.0,0.5)
        drwLstUI.DrawText( "FPS = "+FPS+"   ( "+items+" : "+items2+" )", 10,25, 0.0,0.5)
'        drwLstUI.DrawText( "in  = "+in[0]+":"+in[1], 10,40, 0.0,0.5)
'        drwLstUI.DrawText( "out = "+out[0]+":"+out[1]+":"+out[2], 10,55, 0.0,0.5)

        drwLstUI.DrawText( "Press Cursor-Keys and PageUp/-Down to move the camera", 10,_cH-10 , 0.0,0.5)
        drwLstUI.SetAlpha(0.5)
        drwLstUI.DrawImage(imgLogo,_cW-60,_cH-60,0,0.4,0.4)
        drwLstUI.SetAlpha(1.0)

        canvUI.RenderDrawList(drwLstUI)
        
        canv3D.Flush()
		canvUI.Flush()
		
        Return 0
    End
End

'---------------------------------------------------------------
Function Main:Int()
    New myClass		
    Return 0
End
