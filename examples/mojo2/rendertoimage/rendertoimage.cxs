' Cerberus X - Render to image example
'
' Illustrates the method for drawing on an image

Strict

Import mojo2

Class MyApp Extends App

	Field myCanvas:Canvas
	Field myImage:Image
	Field imageCanvas:Canvas
	Field patternSeed:Float = 1
	
	
	Method OnCreate:Int()
		
		' the main canvas where the final scene is drawn
		myCanvas = New Canvas()
		
		' create an image and set it as a render target which enables us to draw upon it
		myImage = New Image(256, 256)
		imageCanvas = New Canvas(myImage)
		Return 0
	End
	
	
	Method OnRender:Int()
	
		' draw a pattern on the image
		patternSeed += 0.1
		For Local x:Int = 0 Until 128
			For Local y:Int = 0 Until 128
				If (x & y & Int(patternSeed))
					imageCanvas.SetColor(Sin(Millisecs() * 0.1 ) * 0.5 + 0.5, Cos(Millisecs() * 0.1) * 0.5 + 0.5, 0.5)
				Else
					imageCanvas.SetColor(0, 0, 0)
				Endif
				imageCanvas.DrawRect(x * 2, y * 2, 2, 2)
			Next
		Next
		
		' finalize drawing on the image
		imageCanvas.Flush()
		
		' now render to main canvas
		' draw four copies of the final image, using rotation to form a symmetrical pattern
		myCanvas.Clear()	
		myCanvas.DrawImage(myImage, MouseX(), MouseY())
		myCanvas.DrawImage(myImage, MouseX() + 256, MouseY(), -90, 1.0, 1.0)
		myCanvas.DrawImage(myImage, MouseX(), MouseY() + 256, 90, 1.0, 1.0)
		myCanvas.DrawImage(myImage, MouseX() + 256, MouseY() + 256, 180, 1.0, 1.0)
		
		' finalize drawing on the main canvas		
		myCanvas.Flush()
		Return 0
	End
End


Function Main:Int()
	New MyApp()
	Return 0
End

		
		
	
	
	