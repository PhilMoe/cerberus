
# Module brl.admob

The admob module provides simple support for the Google Admob advertising system, which allows you to add advertising to your games.

Please see the [[Admob]] class for more information.

(Note: Admob ads are only currently available on the android and ios targets.)


# Class brl.admob.Admob

The admob class provides simple support for the Google Admob advertising system, which allows you to add advertising to your games.

To use Admob advertising in your game:

+ 1 : Create an Admob account and add a new site/app for your game. You can create an Admob account at http://www.admob.com

+ 2 : Add the app config setting ADMOB_PUBLISHER_ID to your game. This must be set to your app's Admob 'Publisher ID'. See [[App config settings]].

+ 3 : Import the brl.admob module into your game and use the Admob class to enable and disable ads as required.

##Example

<pre>
\#If TARGET<>"android" And TARGET<>"ios"
\#Error "Admob is only supported on Android and iOS targets"
\#End

\#ADMOB_PUBLISHER_ID="abcdabcdabcdabc"							'from your admod account
\#ADMOB_ANDROID_TEST_DEVICE1="TEST_EMULATOR"
\#ADMOB_ANDROID_TEST_DEVICE2="ABCDABCDABCDABCDABCDABCDABCDABCD"	'your device's admob ID for test mode

Import mojo
Import brl.admob

Class MyApp Extends App

	Field admob:Admob
	Field layout:=1
	Field enabled:=True
	
	Method OnCreate()
		admob=Admob.GetAdmob()
		admob.ShowAdView 1,layout
		SetUpdateRate 60
	End
	
	Method OnUpdate()
		If MouseHit( 0 )
			If enabled
				admob.HideAdView
				enabled=False
			Else
				layout+=1
				If layout=7 layout=1
				admob.ShowAdView 1,layout
				enabled=True
			Endif
		End
	End
	
	Method OnRender()
		Cls
		DrawText "Click to toggle ads!",DeviceWidth/2,DeviceHeight/2,.5,.5
	End
	
End

Function Main()
	New MyApp
End
</pre>


# Function GetAdmob:Admob()

Get the global Admob object.


# Method ShowAdView:Void( style:Int=1,layout:Int=2 )

Shows the ad view and enables ads. The @style and @layout parameters should be one of the following:

<table>
<tr><th>Style value</th><th>Ad style</th></tr>
<tr><td>1</td><td>320x50 Banner ad</td></tr>
</table>

<table>
<tr><th>Layout value</th><th>Layout position</th></tr>
<tr><td>1</td><td>top left</td></tr>
<tr><td>2</td><td>top center</td></tr>
<tr><td>3</td><td>top right</td></tr>
<tr><td>4</td><td>bottom left</td></tr>
<tr><td>5</td><td>bottom center</td></tr>
<tr><td>6</td><td>bottom right </td></tr>
</table>


# Method HideAdView:Void()

Hides the ad view and disables ads


# Method AdViewWidth:Int()

Returns the width of the ad view, or 0 if the ad view is hidden.


# Method AdViewHeight:Int()

Returns the height of the ad view, or 0 if the ad view is hidden.

