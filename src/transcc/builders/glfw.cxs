
Import builder

Class GlfwBuilder Extends Builder

	Method New( tcc:TransCC )
		Super.New( tcc )
	End
	
	Method Config:String()
		Local config:=New StringStack
		For Local kv:=Eachin GetConfigVars()
			config.Push "#define CFG_"+kv.Key+" "+kv.Value
		Next
		Return config.Join( "~n" )
	End
	
	'***** GCC *****
	Method MakeGcc:Void()
	
		Local msize:=GetConfigVar( "GLFW_GCC_MSIZE_"+HostOS.ToUpper() )
		
		Local tconfig:=casedConfig+msize
	
		Local dst:="gcc_"+HostOS
		
		CreateDir dst+"/"+tconfig
		CreateDir dst+"/"+tconfig+"/internal"
		CreateDir dst+"/"+tconfig+"/external"
		
		CreateDataDir dst+"/"+tconfig+"/data"
		
		Local main:=LoadString( "main.cpp" )
		
		main=ReplaceBlock( main,"TRANSCODE",transCode )
		main=ReplaceBlock( main,"CONFIG",Config() )
		
		SaveString main,"main.cpp"
		
		If tcc.opt_build

			ChangeDir dst
			CreateDir "build"
			CreateDir "build/"+tconfig
			
			Local ccopts:="",ldopts:="", libopts:="", libcopy:=""

			If msize ccopts+=" -m"+msize;ldopts+=" -m"+msize

			ccopts+=" "+GetConfigVar( "GLFW_GCC_CC_OPTS" ).Replace( ";"," " )
			ldopts+=" "+GetConfigVar( "GLFW_GCC_LD_OPTS" ).Replace( ";"," " )
			libopts+=" "+GetConfigVar( "GLFW_GCC_LIB_OPTS" ).Replace( ";"," " )

			Select ENV_CONFIG
			Case "debug"
				ccopts+=" -O0"
			Case "release"
				ccopts+=" -O3 -DNDEBUG"
				ldopts+=" -s"
			End

			' Process external libs to copy over
			ProcessExternalLibs( CurrentDir()+"\"+casedConfig, msize, libopts )

			' Point the linker in the direction of the prebuilt share libraries
			If HostOS="winnt"
				ldopts+=" -L"+tcc._libs+"Win"+msize
			Endif

			Local cmd:="make"
			If HostOS="winnt" And FileType( tcc.MINGW_PATH+"/bin/mingw32-make.exe" ) cmd="mingw32-make"

			Execute cmd+" CCOPTS=~q"+ccopts+"~q LDOPTS=~q"+ldopts+"~q LIBOPTS=~q"+libopts+"~q OUT=~q"+tconfig+"/CerberusGame~q"

			If tcc.opt_run

				ChangeDir tconfig

				If HostOS="winnt"
					Execute "CerberusGame"
				Else
					Execute "./CerberusGame"
				Endif
			Endif
		Endif
			
	End
	
	'***** Vc2010 *****
	Method MakeVc2010:Void()
	
		CreateDir "vc2010/"+casedConfig
		CreateDir "vc2010/"+casedConfig+"/internal"
		CreateDir "vc2010/"+casedConfig+"/external"
		
		CreateDataDir "vc2010/"+casedConfig+"/data"
		
		Local main:=LoadString( "main.cpp" )
		
		main=ReplaceBlock( main,"TRANSCODE",transCode )
		main=ReplaceBlock( main,"CONFIG",Config() )
		
		SaveString main,"main.cpp"
		
		If tcc.opt_build

			ChangeDir "vc2010"

			Execute "~q"+tcc.MSBUILD_PATH+"~q /p:Configuration="+casedConfig+" /p:Platform=Win32 CerberusGame.sln"
			
			If tcc.opt_run
			
				ChangeDir casedConfig

				Execute "CerberusGame"
				
			Endif
		Endif
	End

	'***** Msvc *****
	Method MakeMsvc:Void()
	
		CreateDir "msvc/"+casedConfig
		CreateDir "msvc/"+casedConfig+"/internal"
		CreateDir "msvc/"+casedConfig+"/external"
		
		CreateDataDir "msvc/"+casedConfig+"/data"
		
		Local main:=LoadString( "main.cpp" )
		
		main=ReplaceBlock( main,"TRANSCODE",transCode )
		main=ReplaceBlock( main,"CONFIG",Config() )
		
		SaveString main,"main.cpp"
		
		If tcc.opt_build

			ChangeDir "msvc"

			Execute "~q"+tcc.MSBUILD_PATH+"~q /p:Configuration="+casedConfig'+" /p:Platform=win32 CerberusGame.sln"
			
			If tcc.opt_run
			
				ChangeDir casedConfig

				Execute "CerberusGame"
				
			Endif
		Endif
	End

	'***** Xcode *****	
	Method MakeXcode:Void()

		CreateDataDir "xcode/data"

		Local main:=LoadString( "main.cpp" )
		
		main=ReplaceBlock( main,"TRANSCODE",transCode )
		main=ReplaceBlock( main,"CONFIG",Config() )
		
		SaveString main,"main.cpp"
		
		If tcc.opt_build
		
			ChangeDir "xcode"
			
'			Execute "set -o pipefail && xcodebuild -configuration "+casedConfig+" | egrep -A 5 ~q(error|warning):~q"
			Execute "xcodebuild -configuration "+casedConfig
			
			If tcc.opt_run
			
				ChangeDir "build/"+casedConfig
				ChangeDir "CerberusGame.app/Contents/MacOS"
				
				Execute "./CerberusGame"
			Endif
		Endif
	End
	
	'***** Builder *****	
	Method IsValid:Bool()
		Select HostOS
		Case "winnt"
			If tcc.MINGW_PATH Or tcc.MSBUILD_PATH Return True
		Default
			Return True
		End
		Return False
	End
	
	Method Begin:Void()
		ENV_LANG="cpp"
		_trans=New CppTranslator
	End
	
	Method MakeTarget:Void()
		Select HostOS
		Case "winnt"
			If GetConfigVar( "GLFW_USE_MINGW" )="1" And tcc.MINGW_PATH
				MakeGcc
			Else If FileType( "vc2010" )=FILETYPE_DIR
				MakeVc2010
			Else If FileType( "msvc" )=FILETYPE_DIR
				MakeMsvc
			Else If tcc.MINGW_PATH
				MakeGcc
			Endif
		Case "macos"
			MakeXcode
		Case "linux"
			MakeGcc
		End
	End

	' Process external shared libraries	to copy
	Method ProcessExternalLibs( config:String, arch:String, link:String )
		Local libStr:String = GetConfigVar( "GLFW_COPY_LIBS" ).ToLower()
		If libStr = "" Then Return

		Local libcopy:String[] = libStr.Split( ";" )
		Local dst:String = config+arch

		For Local i:= Eachin libcopy
			If HostOS="winnt"
				If i <> ""
					If FileType( dst+"/"+i+".dll" ) = FILETYPE_FILE Continue

					If CopyFile( tcc._libs+"Win"+arch+"/"+i+".dll", dst+"/"+i+".dll" ) = 1
						Print "Copied "+i+".dll to "+dst
					Else
						Print "Failed to copy "+i+" to "+dst
					EndIf

					If FileType( tcc._libs+"/"+i+"_COPYING" )<> FILETYPE_NONE
						If CopyFile( tcc._libs+"/"+i+"_COPYING", dst+"/"+i+"_COPYING" ) = 1
							Print "Copied "+i+"_COPYING to "+dst
						Else
							Print "Failed to copy "+i+"_COPYING To "+dst
						Endif
					Endif

					If FileType( tcc._libs+"/"+i+"_LICENCE" )<> FILETYPE_NONE
						If CopyFile( tcc._libs+"/"+i+"_LICENCE", dst+"/"+i+"_LICENCE" ) = 1
							Print "Copied "+i+"_LICENCE to "+dst
						Else
							Print "Failed to copy "+i+"_COPYING To "+dst
						Endif
					Endif
				Endif
			EndIf
		Next
	End
End
