
Import builder

Class GlfwBuilder Extends Builder

	Method New( tcc:TransCC )
		Super.New( tcc )
	End
	
	Method Config:String()
		Local config:=New StringStack
		For Local key:=Eachin GetConfigVarKeys()
			config.Push "#define CFG_"+key+" "+GetConfigVar(key)
		Next
		Return config.Join( "~n" )
	End
	
	'***** GCC *****
	Method MakeGcc:Void()
	
		Local msize:=GetConfigVar( "GLFW_GCC_MSIZE_"+HostOS.ToUpper() )
		
		Local tconfig:=casedConfig+msize
	
		Local dst:="gcc_"+HostOS
		
		CreateDir dst+"/"+tconfig
		CreateDir dst+"/"+tconfig+"/internal"
		CreateDir dst+"/"+tconfig+"/external"
		
'		If msize=32
'			If FileType(dst+"/Makefile32")=FILETYPE_FILE
'				CopyFile(dst+"/Makefile32",dst+"/Makefile")
'			Endif
'		Else
'			If FileType(dst+"/Makefile64")=FILETYPE_FILE
'				CopyFile(dst+"/Makefile64",dst+"/Makefile")
'			Endif
'		Endif
		If FileType(dst+"/build/"+tconfig+"/resource.o")=FILETYPE_FILE
			DeleteFile(dst+"/build/"+tconfig+"/resource.o")
		Endif
		
		CreateDataDir dst+"/"+tconfig+"/data"

		CopySourceFiles(".")
		
		' need to rewrite resources.rc so that icon is rebuilt
		CopyIcon(GetConfigVar( "GLFW_APP_ICON"),CurrentDir()+"/cerberus.ico")
		
		Local main:=LoadString( "main.cpp" )
		
		main=ReplaceBlock( main,"TRANSCODE",transCode )
		main=ReplaceBlock( main,"CONFIG",Config() )
		
		SaveString main,"main.cpp"
		
		If tcc.opt_build

			ChangeDir dst
			CreateDir "build"
			CreateDir "build/"+tconfig
			
			Local ccopts:="",ldopts:="", libopts:="", libcopy:="", srcopts:= "", vpathopts:=""

			If msize ccopts+=" -m"+msize;ldopts+=" -m"+msize

			UnifyConfigVarSeparator( "GLFW_GCC_CC_OPTS", " ", ["|",";"] )
			UnifyConfigVarSeparator( "GLFW_GCC_LD_OPTS", " ", ["|",";"] )
			UnifyConfigVarSeparator( "GLFW_GCC_LIB_OPTS", " ", ["|",";"] )
			UnifyConfigVarSeparator( "GLFW_GCC_SRC_OPTS", " ", ["|",";"] )
			UnifyConfigVarSeparator( "GLFW_GCC_VPATH_OPTS", " ", ["|",";"] )

			ccopts+=" "+GetConfigVar( "GLFW_GCC_CC_OPTS" )
			ldopts+=" "+GetConfigVar( "GLFW_GCC_LD_OPTS" )
			libopts+=" "+GetConfigVar( "GLFW_GCC_LIB_OPTS" )
			srcopts+=" "+GetConfigVar( "GLFW_GCC_SRC_OPTS" )
			vpathopts+=" "+GetConfigVar( "GLFW_GCC_VPATH_OPTS" )

			Select ENV_CONFIG
			Case "debug"
				ccopts+=" -O0"
			Case "release"
				ccopts+=" -O3 -DNDEBUG"
				ldopts+=" -s"
			End

			' Process external libs to copy over
			ProcessExternalLibs( CurrentDir()+"\"+casedConfig, msize, libopts )

			' Point the linker in the direction of the prebuilt share libraries
			If HostOS="winnt"
				ldopts+=" -L"+tcc._libs+"Win"+msize
			Endif

			Local cmd:="make"
			If HostOS="winnt" And FileType( tcc.MINGW_PATH+"/bin/mingw32-make.exe" ) cmd="mingw32-make"

			'Execute cmd+" CCOPTS=~q"+ccopts+"~q LDOPTS=~q"+ldopts+"~q LIBOPTS=~q"+libopts+"~q OUT=~q"+tconfig+"/CerberusGame~q"
			'Execute cmd+" ARCH=~q"+msize+"~q CCOPTS=~q"+ccopts+"~q LDOPTS=~q"+ldopts+"~q LIBOPTS=~q"+libopts+"~q OUT=~q"+tconfig+"/CerberusGame~q"
			Execute cmd+" ARCH=~q"+msize+"~q CCOPTS=~q"+ccopts+"~q LDOPTS=~q"+ldopts+"~q LIBOPTS=~q"+libopts+"~q SRCOPTS=~q"+srcopts+"~q VPATHOPTS=~q"+vpathopts+"~q OUT=~q"+tconfig+"/CerberusGame~q"

			If tcc.opt_run

				ChangeDir tconfig

				If HostOS="winnt"
					Execute "CerberusGame"
				Else
					Execute "./CerberusGame"
				Endif
			Endif
		Endif
			
	End
	
	'***** Vc2010 *****
	Method MakeVc2010:Void()
	
		CreateDir "vc2010/"+casedConfig
		CreateDir "vc2010/"+casedConfig+"/internal"
		CreateDir "vc2010/"+casedConfig+"/external"
		
		CreateDataDir "vc2010/"+casedConfig+"/data"
		
		Local main:=LoadString( "main.cpp" )
		
		main=ReplaceBlock( main,"TRANSCODE",transCode )
		main=ReplaceBlock( main,"CONFIG",Config() )
		
		SaveString main,"main.cpp"
		
		If tcc.opt_build

			ChangeDir "vc2010"

			Execute "~q"+tcc.MSBUILD_PATH+"~q /p:Configuration="+casedConfig+" /p:Platform=Win32 CerberusGame.sln"
			
			If tcc.opt_run
			
				ChangeDir casedConfig

				Execute "CerberusGame"
				
			Endif
		Endif
	End

	'***** Msvc *****
	Method MakeMsvc:Void()

		Local ccopts:="",ldopts:="", libopts:="", libcopy:="", srcopts:= "", vpathopts:=""
		Local msize:=GetConfigVar( "GLFW_GCC_MSIZE_"+HostOS.ToUpper() )

		'If msize ccopts+=" -m"+msize;ldopts+=" -m"+msize

		UnifyConfigVarSeparator( "GLFW_GCC_CC_OPTS", " ", ["|",";"] )
		UnifyConfigVarSeparator( "GLFW_GCC_LD_OPTS", " ", ["|",";"] )
		UnifyConfigVarSeparator( "GLFW_GCC_LIB_OPTS", " ", ["|",";"] )
		UnifyConfigVarSeparator( "GLFW_GCC_SRC_OPTS", " ", ["|",";"] )
		UnifyConfigVarSeparator( "GLFW_GCC_VPATH_OPTS", " ", ["|",";"] )

		'ccopts+=" "+GetConfigVar( "GLFW_GCC_CC_OPTS" )
		'ldopts+=" "+GetConfigVar( "GLFW_GCC_LD_OPTS" )
		libopts+=" "+GetConfigVar( "GLFW_GCC_LIB_OPTS" )
		'srcopts+=" "+GetConfigVar( "GLFW_GCC_SRC_OPTS" )
		'vpathopts+=" "+GetConfigVar( "GLFW_GCC_VPATH_OPTS" )
			
		CreateDir "msvc/"+casedConfig+msize
		CreateDir "msvc/"+casedConfig+msize+"/internal"
		CreateDir "msvc/"+casedConfig+msize+"/external"
		
		CreateDataDir "msvc/"+casedConfig+msize+"/data"
		
		' need to rewrite resources.rc so that icon is rebuilt
		CopyIcon(GetConfigVar( "GLFW_APP_ICON"),CurrentDir()+"\cerberus.ico")
		
		Local main:=LoadString( "main.cpp" )
		
		main=ReplaceBlock( main,"TRANSCODE",transCode )
		main=ReplaceBlock( main,"CONFIG",Config() )
		
		SaveString main,"main.cpp"
		
		' Process external libs to copy over
		ProcessExternalLibs( "msvc/"+casedConfig, msize, libopts )
		
		If tcc.opt_build

			ChangeDir "msvc"
			Local sBuild:String
			If msize = 32
				sBuild = "~q"+tcc.MSBUILD_PATH+"~q /p:Configuration="+casedConfig+" /p:Platform=win32"
			Else
				sBuild = "~q"+tcc.MSBUILD_PATH+"~q /p:Configuration="+casedConfig+" /p:Platform=x64"
			Endif
			
			'Execute "~q"+tcc.MSBUILD_PATH+"~q /p:Configuration="+casedConfig+msize'+" /p:Platform=win32 CerberusGame.sln"
			Execute sBuild
			
			If tcc.opt_run
			
				ChangeDir casedConfig+msize

				Execute "CerberusGame"
				
			Endif
		Endif
	End

	'***** Xcode *****	
	Method MakeXcode:Void()

		CreateDataDir "xcode/data"
		
		CopySourceFiles("xcode")

		Local main:=LoadString( "main.cpp" )
		
		main=ReplaceBlock( main,"TRANSCODE",transCode )
		main=ReplaceBlock( main,"CONFIG",Config() )
		
		SaveString main,"main.cpp"
		
		CopyIcon(GetConfigVar( "GLFW_APP_ICON"),CurrentDir()+"/xcode/cerberus.icns")

		If tcc.opt_build
		
			ChangeDir "xcode"
			
'			Execute "set -o pipefail && xcodebuild -configuration "+casedConfig+" | egrep -A 5 ~q(error|warning):~q"
			Execute "xcodebuild -configuration "+casedConfig
			
			If tcc.opt_run
			
				ChangeDir "build/"+casedConfig
				ChangeDir "CerberusGame.app/Contents/MacOS"
				
				Execute "./CerberusGame"
			Endif
		Endif
	End
	
	'***** Builder *****	
	Method IsValid:Bool()
		Select HostOS
		Case "winnt"
			If tcc.MINGW_PATH Or tcc.MSBUILD_PATH Return True
		Default
			Return True
		End
		Return False
	End
	
	Method Begin:Void()
		ENV_LANG="cpp"
		_trans=New CppTranslator
	End
	
	Method MakeTarget:Void()
		Select HostOS
		Case "winnt"
			If GetConfigVar( "GLFW_USE_MINGW" )="1" And tcc.MINGW_PATH
				MakeGcc
			Else If FileType( "vc2010" )=FILETYPE_DIR
				MakeVc2010
			Else If FileType( "msvc" )=FILETYPE_DIR
				MakeMsvc
			Else If tcc.MINGW_PATH
				MakeGcc
			Endif
		Case "macos"
			MakeXcode
		Case "linux"
			MakeGcc
		End
	End

	' Process external shared libraries	to copy
	Method ProcessExternalLibs( config:String, arch:String, link:String )
		Local libStr:String
		Local msize:=GetConfigVar( "GLFW_GCC_MSIZE_"+HostOS.ToUpper() )
		If msize = 64
			UnifyConfigVarSeparator( "GLFW_COPY_LIBS", "|", ["|",";"] )
			libStr = GetConfigVar( "GLFW_COPY_LIBS" ).ToLower()
		Else
			UnifyConfigVarSeparator( "GLFW_COPY_LIBS_32", "|", ["|",";"] )
			libStr = GetConfigVar( "GLFW_COPY_LIBS_32" ).ToLower()
		Endif
		If libStr = "" Then Return

		Local libcopy:String[] = libStr.Split( "|" )
		Local dst:String = config+arch
		For Local i:= Eachin libcopy
			If HostOS="winnt"
				If i <> ""
					If FileType( dst+"/"+i+".dll" ) = FILETYPE_FILE Continue

					If CopyFile( tcc._libs+"Win"+arch+"/"+i+".dll", dst+"/"+i+".dll" ) = 1
						Print "Copied "+i+".dll to "+dst
					Else
						Print "Failed to copy "+i+" to "+dst
					EndIf

					If FileType( tcc._libs+"/"+i+"_COPYING" )<> FILETYPE_NONE
						If CopyFile( tcc._libs+"/"+i+"_COPYING", dst+"/"+i+"_COPYING" ) = 1
							Print "Copied "+i+"_COPYING to "+dst
						Else
							Print "Failed to copy "+i+"_COPYING To "+dst
						Endif
					Endif

					If FileType( tcc._libs+"/"+i+"_LICENCE" )<> FILETYPE_NONE
						If CopyFile( tcc._libs+"/"+i+"_LICENCE", dst+"/"+i+"_LICENCE" ) = 1
							Print "Copied "+i+"_LICENCE to "+dst
						Else
							Print "Failed to copy "+i+"_COPYING To "+dst
						Endif
					Endif
				Endif
			EndIf
		Next
	End
End
