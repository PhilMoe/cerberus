
Import builder

Import brl.filestream

Private

Public

Class Html5Builder Extends Builder

	Method New( tcc:TransCC )
		Super.New( tcc )
	End
	
	Method Config:String()
		Local config:=New StringStack
		For Local key:=Eachin GetConfigVarKeys()
			config.Push "CFG_"+key+"="+Enquote( GetConfigVar(key),"js" )+";"
		Next
		Return config.Join( "~n" )
	End
	
	Method MetaData:String()
		Local meta:=New StringStack
		For Local kv:=Eachin dataFiles
			Local src:=kv.Key
			Local ext:=ExtractExt( src ).ToLower()
			Select ext
			Case "png","jpg","gif"
				_html5_Info_Width=0
				_html5_Info_Height=0
				Select ext
				Case "png" GetInfo_PNG( src )
				Case "jpg" GetInfo_JPG( src )
				Case "gif" GetInfo_GIF( src )
				End
				If _html5_Info_Width=0 Or _html5_Info_Height=0 Die "Unable to load image file '"+src+"'."
				meta.Push "["+kv.Value+"];type=image/"+ext+";"
				meta.Push "width="+_html5_Info_Width+";"
				meta.Push "height="+_html5_Info_Height+";"
				meta.Push "\n"
			End
		Next
		Return meta.Join("")
	End
	
	Method IsValid:Bool()
		Return True
	End
	
	Method Begin:Void()
		ENV_LANG="js"
		_trans=New JsTranslator
	End
	
	Method MakeTarget:Void()

		CreateDataDir "data"

		Local meta:="var META_DATA=~q"+MetaData()+"~q;~n"
		
		Local main:=LoadString( "main.js" )
		main=ReplaceBlock( main,"TRANSCODE",transCode )
		main=ReplaceBlock( main,"METADATA",meta )
		main=ReplaceBlock( main,"CONFIG",Config() )
		
		SaveString main,"main.js"

		CopyIcon(GetConfigVar("HTML5_APP_ICON"),CurrentDir()+"\favicon.ico")
				

		Local game:=LoadString( "CerberusGame.html" )
		game=ReplaceBlock( game,"APP_TITLE","<title>"+GetConfigVar("HTML5_APP_TITLE")+"</title>","~n<!--" )

		Local cvrep:String
		cvrep  =        "var CANVAS_RESIZE_MODE=" + GetConfigVar("HTML5_CANVAS_RESIZE_MODE") + ";	//0=locked, 1=stretch, 2=resize"
		cvrep += "~n" + "var CANVAS_WIDTH="+GetConfigVar("HTML5_CANVAS_WIDTH")+";"
		cvrep += "~n" + "var CANVAS_HEIGHT="+GetConfigVar("HTML5_CANVAS_HEIGHT")+";"
		cvrep += "~n" + "var CONSOLE_SHOW="+GetConfigVar("HTML5_CONSOLE_SHOW")+";"
		game=ReplaceBlock( game,"CANVAS",cvrep,"~n<!--" )

		
		'game = game.Replace("%%HTML5_CONSOLE_SHOW%%", GetConfigVar("HTML5_CONSOLE_SHOW") )
		'game = game.Replace("%%HTML5_APP_TITLE%%", GetConfigVar("HTML5_APP_TITLE") )
		'game = game.Replace("%%HTML5_CANVAS_WIDTH%%", GetConfigVar("HTML5_CANVAS_WIDTH") )
		'game = game.Replace("%%HTML5_CANVAS_HEIGHT%%", GetConfigVar("HTML5_CANVAS_HEIGHT") )
		'game = game.Replace("%%HTML5_CANVAS_RESIZE_MODE%%", GetConfigVar("HTML5_CANVAS_RESIZE_MODE") )
		SaveString game, GetConfigVar("HTML5_APP_FILENAME")


		If tcc.opt_run
			Local p:=RealPath( GetConfigVar("HTML5_APP_FILENAME") )
			Local t:=tcc.HTML_PLAYER+" ~q"+p+"~q"
			Execute t,False
		Endif
	End
	
End