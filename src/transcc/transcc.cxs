' stdcpp app 'transcc' - driver program for the Cerberus translator.
'
' Placed into the public domain 24/02/2011.
' No warranty implied; use at your own risk.

Import trans
Import builders

#If CONFIG="debug"
	'	DEBUG_TEST_CFG: Release,Debug
	'	DEBUG_TARGET: Desktop,Android,iOS,iOS Metal,HTML
	'	DEBUG_TEST: TransCC,Reflection,Mojo,Mojo2,Audio
		
	' Change the preprocessor directives to select the example to run for debugging transcc
	#DEBUG_TEST_CFG="Release"
	#DEBUG_TEST="Audio"
	#DEBUG_TARGET="HTML"
	Import debugconf
	
	Const VERSION:="2023-10-14 (DEBUG)"
#Else
    Const VERSION:="2023-10-14"
#Endif

Function Main()
    Local tcc:=New TransCC
    #If CONFIG="debug"
        SetEnv "CERBERUS_DIR",RealPath( ExtractDir( AppPath )+"/../../../../" )
        SetEnv "CXS_FILE",GetEnv( "CERBERUS_DIR" )+TEST_PATH+"/"+TEST_FILE+"/"+TEST_FILE+".cxs"
      
        Local str:String, args$[]=[ GetEnv( "CERBERUS_DIR" )+"/bin/transcc_"+HostOS(),
                                    "-target="+TEST_TARGET,
                                    "-config="+TEST_CONFIG,
                                    TEST_BUILD,
                                    GetEnv( "CXS_FILE" ) ]
        For Local i:=Eachin args
            str+=i+" "
        Next
        DebugLog "~nRunning in debug mode:~n"+str
        tcc.Run args
    #Else
        SetEnv "CERBERUS_DIR",RealPath( ExtractDir( AppPath )+"/.." )
        tcc.Run AppArgs
    #Endif
End

Function Die( msg:String )
    Print "TRANS FAILED: "+msg
    ExitApp -1
End

Function StripQuotes:String( str:String )
    If str.Length>=2 And str.StartsWith( "~q" ) And str.EndsWith( "~q" ) Return str[1..-1]
    Return str
End


' Checks, if source filename is valid (without space characters).
Function IsValidSourceFilename:Bool( sourcePath:String )
	Local forbiddenChars:String[] = [" "] 'There are eventually more to come.
    Local sourceFilename:String
    
    sourceFilename = StripDir(StripExt(sourcePath))

    For Local chr:String = Eachin forbiddenChars
		If sourceFilename.Find(chr) >= 0 Return False
	Next
	
	Return True
End


Function ReplaceEnv:String( str:String )
    Local bits:=New StringStack

    Repeat
        Local i=str.Find( "${" )
        If i=-1 Exit

        Local e=str.Find( "}",i+2 )
        If e=-1 Exit
      
        If i>=2 And str[i-2..i]="//"
            bits.Push str[..e+1]
            str=str[e+1..]
            Continue
        Endif
      
        Local t:=str[i+2..e]

        Local v:=GetConfigVar(t)
        If Not v v=GetEnv(t)
      
        bits.Push str[..i]
        bits.Push v
      
        str=str[e+1..]
    Forever
    If bits.IsEmpty() Return str
  
    bits.Push str
    Return bits.Join( "" )
End

Function ReplaceBlock:String( text:String,tag:String,repText:String,mark:String="~n//" )

    'find begin tag
    Local beginTag:=mark+"${"+tag+"_BEGIN}"
    Local i=text.Find( beginTag )
    If i=-1 Die "Error updating target project - can't find block begin tag '"+tag+"'. You may need to delete target .build directory."
    i+=beginTag.Length
    While i<text.Length And text[i-1]<>10
        i+=1
    Wend
  
    'find end tag
    Local endTag:=mark+"${"+tag+"_END}"
    Local i2=text.Find( endTag,i-1 )
    If i2=-1 Die "Error updating target project - can't find block end tag '"+tag+"'."
    If Not repText Or repText[repText.Length-1]=10 i2+=1
  
    Return text[..i]+repText+text[i2..]
End

Function MatchPathAlt:Bool( text:String,alt:String )

    If Not alt.Contains( "*" ) Return alt=text
  
    Local bits:=alt.Split( "*" )
    If Not text.StartsWith( bits[0] ) Return False

    Local n:=bits.Length-1
    Local i:=bits[0].Length
    For Local j:=1 Until n
        Local bit:=bits[j]
        i=text.Find( bit,i )
        If i=-1 Return False
        i+=bit.Length
    Next

    Return text[i..].EndsWith( bits[n] )
End

Function MatchPath:Bool( text:String,pattern:String )

    text="/"+text
    Local alts:=pattern.Split( "|" )
    Local match:=False

    For Local alt:=Eachin alts
        If Not alt Continue
      
        If alt.StartsWith( "!" )
            If MatchPathAlt( text,alt[1..] ) Return False
        Else
            If MatchPathAlt( text,alt ) match=True
        Endif
    Next
  
    Return match
End

Function ExpandEnv:String( text:String )
	
	Local resolved:="", char:="", idx1:=0, terminator:=0, idx2:=0
	While idx1<text.Length
		char=String.FromChar( text[idx1] )
		
		Select char
		' MS Windows style environment expansion
		Case "%"
			terminator=text.Find( "%",idx1+1 )
			resolved+=GetEnv(text[idx1+1..terminator])
			If terminator<0 terminator=text.Length
			idx1+=(terminator+1)-idx1
		
		' Unix style environment expansion
		Case "$"
			terminator=-1
			idx2=idx1
			
			' Look ahead for one of the known termination characters.
			While idx2<text.Length And terminator<0
				If text[idx2]="/"[0] Or text[idx2]=":"[0] Or text[idx2]=";"[0] terminator=idx2
				idx2+=1
			Wend
			If terminator<0 terminator=text.Length
			resolved+=GetEnv(text[idx1+1..terminator])
			idx1+=(terminator)-idx1
			
		Default
			resolved+=char
			idx1+=1
			
		End
	Wend
	
	Return resolved
End

Function CreateDirs( path:String,delim:String="/" )
	
	Print "Creating output directory structure:~n"+path
	Local dirs:=path.Split( delim ), currentDir:=CurrentDir
	
	' Special case for Linux and macOS to ensure that the root directory is set.
	If HostOS<>"winnt"
		If dirs[0]="" Then dirs[0]="/"
	Endif
		
	For Local i:=Eachin dirs

		Select FileType( i )
		Case FILETYPE_DIR
			Print "Directory already exists:~n"+RealPath( i )
		Case FILETYPE_FILE
			Die "Directory creation error: Path is a regular file. Cannot continue.~n"+RealPath( i )
		Default
			If CreateDir( i.Trim ) Print "Created directory:~n"+RealPath( i )
		End
		ChangeDir i.Trim
	Next
	ChangeDir currentDir
End

Class Target
    Field dir:String
    Field name:String
    Field system:String
    Field builder:Builder
    Field path:String
    Field lang:String
  	Field buildscript:String
  	
    Method New( dir:String,name:String,system:String,builder:Builder, lang:String, buildscript:String, path:String )
        Self.dir=dir
        Self.name=name
        Self.system=system
        Self.builder=builder
        Self.lang=lang
        Self.buildscript=buildscript
        Self.path=path
    End
End

Class TransCC

    'cmd line args
    Field opt_safe:Bool
    Field opt_clean:Bool
    Field opt_check:Bool
    Field opt_update:Bool
    Field opt_build:Bool
    Field opt_run:Bool

    Field opt_srcpath:String
    Field opt_cfgfile:String
    Field opt_output:String
    Field opt_config:String
    Field opt_casedcfg:String
    Field opt_target:String
    Field opt_modpath:String
    Field opt_targetpath:String
    Field opt_builddir:String
  
    'config file
    Field ANDROID_PATH:String
    Field ANDROID_NDK_PATH:String
    Field ANT_PATH:String
    Field JDK_PATH:String
    Field FLEX_PATH:String
    Field MINGW_PATH:String
    Field MSBUILD_PATH:String
    Field PSS_PATH:String
    Field PSM_PATH:String
    Field AGK_PATH:String
    Field HTML_PLAYER:String
    Field FLASH_PLAYER:String
  
    Field args:String[]
    Field cerberusdir:String
    Field target:Target
  
    Field _builders:=New StringMap<Builder>
    Field _targets:=New StringMap<Target>
    Field _libs:String
  
    Method Run:Void( args:String[] )

        Self.args=args
      
        Print "TRANS cerberus compiler V"+VERSION
      
        cerberusdir=GetEnv( "CERBERUS_DIR" )
        _libs=cerberusdir+"/libs/"

        SetEnv "CERBERUSDIR",cerberusdir
        SetEnv "MONKEYDIR",cerberusdir 'this is for Jungle compatibility
        SetEnv "TRANSDIR",cerberusdir+"/bin"
  
        ParseArgs
      
        LoadConfig
      
        EnumBuilders
      
		If opt_targetpath.Length <= 1 Then opt_targetpath = "targets"

		Local tpaths:= opt_targetpath.Split(";")
		For Local tpath := Eachin tpaths
        	EnumTargets tpath
        Next
      
        If args.Length<2
            Local valid:=""
            For Local it:=Eachin _targets
                valid+=" "+it.Key.Replace( " ","_" )
            Next
            Print "TRANS Usage: transcc [-update] [-build] [-run] [-clean] [-config=...] [-target=...] [-cfgfile=...] [-builddir=...] [-modpath=...]  [-targetpath=...] <main_cerberus_source_file>"
            Print "Valid targets:"+valid
            Print "Valid configs: debug release"
            ExitApp 0
        Endif
      
        target=_targets.Get( opt_target.Replace( "_"," " ) )
        If Not target Die "Invalid target"
      
        target.builder.Make
    End

    Method GetReleaseVersion:String()
        Local f:=LoadString( cerberusdir+"/VERSIONS.TXT" )
        For Local t:=Eachin f.Split( "~n" )
            t=t.Trim()
            If t.StartsWith( "***** v" ) And t.EndsWith( " *****" ) Return t[6..-6]
        Next
        Return ""
    End
  
    Method EnumBuilders:Void()
        For Local it:=Eachin Builders( Self )
            If it.Value.IsValid() _builders.Set it.Key,it.Value
        Next
    End
  
    Method EnumTargets:Void( dir:String )
  
        Local p:=cerberusdir+"/"+dir
        If FileType(p) <> FILETYPE_DIR Then p = dir
        For Local f:=Eachin LoadDir( p )
            Local t:=p+"/"+f+"/target.cxs"
            If FileType(t)<>FILETYPE_FILE
                t=p+"/"+f+"/TARGET.MONKEY"    'backwards compatibility
                If FileType(t)<>FILETYPE_FILE Continue
            End If
          
            PushConfigScope
          
            PreProcess t

            Local name:=GetConfigVar( "TARGET_NAME" )
            If name
'Print "TARGET_NAME="+name
                Local system:=GetConfigVar( "TARGET_SYSTEM" )
                If system
'Print "TARGET_SYSTEM="+system
                    Local builder:=_builders.Get( GetConfigVar( "TARGET_BUILDER" ) )
                    If builder
'Print "TARGET_BUILDER="+GetConfigVar( "TARGET_BUILDER" )
                        ENV_LANG = GetConfigVar( "TARGET_LANG" )
'Print "TARGET_LANG="+ENV_LANG
                        ENV_CUSTOMBUILDSCRIPT = p+"/"+f+"/cxbuild.txt"
                        Local host:=GetConfigVar( "TARGET_HOST" )
                        If Not host Or host=HostOS Or host.Find(HostOS,0)>=0
'Print "TARGET_HOST="+host
                            _targets.Set name,New Target( f,name,system,builder, ENV_LANG, ENV_CUSTOMBUILDSCRIPT, p+"/"+f )
                        Endif
                    Endif
                Endif
            Endif
          
            PopConfigScope
          
        Next
    End
  
    Method ParseArgs:Void()
  
        If args.Length>1 opt_srcpath=StripQuotes( args[args.Length-1].Trim() )
        If Not IsValidSourceFilename(opt_srcpath) 
            Die("Source filename contains space characters: " + opt_srcpath)		
        EndIf

        For Local i:=1 Until args.Length-1
      
            Local arg:=args[i].Trim(),rhs:=""
            Local j:=arg.Find( "=" )
            If j<>-1
                rhs=StripQuotes( arg[j+1..] )
                arg=arg[..j]
            Endif
      
            If j=-1
                Select arg.ToLower()
                Case "-safe"
                    opt_safe=True
                Case "-clean"
                    opt_clean=True
                Case "-check"
                    opt_check=True
                Case "-update"
                    opt_check=True
                    opt_update=True
                Case "-build"
                    opt_check=True
                    opt_update=True
                    opt_build=True
                Case "-run"
                    opt_check=True
                    opt_update=True
                    opt_build=True
                    opt_run=True
                Default
                    Die "Unrecognized command line option: "+arg
                End
            Else If arg.StartsWith( "-" )
                Select arg.ToLower()
                Case "-cfgfile"
                    opt_cfgfile=rhs
                Case "-output"
                    opt_output=rhs
                Case "-config"
                    opt_config=rhs.ToLower()
                Case "-target"
                    opt_target=rhs
                Case "-modpath"
                    opt_modpath=rhs
                Case "-targetpath"
                    opt_targetpath=rhs
                Case "-builddir"
                    opt_builddir=rhs
                Default
                    Die "Unrecognized command line option: "+arg
                End
            Else If arg.StartsWith( "+" )
                SetConfigVar arg[1..],rhs
            Else
                Die "Command line arg error: "+arg
            End
        Next
      
    End

    Method LoadConfig:Void()
  
        Local cfgpath:=cerberusdir+"/bin/"
        If opt_cfgfile
            cfgpath+=opt_cfgfile
        Else
            cfgpath+="config."+HostOS+".txt"
        Endif
        If FileType( cfgpath )<>FILETYPE_FILE Die "Failed to open config file"
  
        Local cfg:=LoadString( cfgpath )
          
        For Local line:=Eachin cfg.Split( "~n" )
      
            line=line.Trim()
            If Not line Or line.StartsWith( "'" ) Continue
          
            Local i=line.Find( "=" )
            If i=-1 Die "Error in config file, line="+line
          
            Local lhs:=line[..i].Trim()
            Local rhs:=line[i+1..].Trim()
          
            rhs=ReplaceEnv( rhs )
          
            Local path:=StripQuotes( rhs )
  
            While path.EndsWith( "/" ) Or path.EndsWith( "\" )
                path=path[..-1]
            Wend
          
            Select lhs
            Case "MODPATH"
                If Not opt_modpath
                    opt_modpath=path
                Endif
            Case "TARGETPATH"
                If Not opt_targetpath
                    opt_targetpath=path
                Endif
            Case "ANDROID_PATH"
                If Not ANDROID_PATH And FileType( path )=FILETYPE_DIR
                    ANDROID_PATH=path
                Endif
            Case "ANDROID_NDK_PATH"
                If Not ANDROID_NDK_PATH And FileType( path )=FILETYPE_DIR
                    ANDROID_NDK_PATH=path
                Endif
            Case "JDK_PATH"
                If Not JDK_PATH And FileType( path )=FILETYPE_DIR
                    JDK_PATH=path
                Endif
            Case "ANT_PATH"
                If Not ANT_PATH And FileType( path )=FILETYPE_DIR
                    ANT_PATH=path
                Endif
            Case "FLEX_PATH"
                If Not FLEX_PATH And FileType( path )=FILETYPE_DIR
                    FLEX_PATH=path
                Endif
            Case "MINGW_PATH"
                If Not MINGW_PATH And FileType( path )=FILETYPE_DIR
                    MINGW_PATH=path
                Endif
            Case "PSM_PATH"
                If Not PSM_PATH And FileType( path )=FILETYPE_DIR
                    PSM_PATH=path
                Endif
            Case "MSBUILD_PATH"
                If Not MSBUILD_PATH And FileType( path )=FILETYPE_FILE
                    MSBUILD_PATH=path
                Endif         
            Case "AGK_PATH"
                If Not AGK_PATH And FileType( path )=FILETYPE_DIR
                    AGK_PATH=path
                Endif
            Case "HTML_PLAYER"
                HTML_PLAYER=rhs
            Case "FLASH_PLAYER"
                FLASH_PLAYER=rhs
            Default
                Print "Trans: ignoring unrecognized config var: "+lhs
            End
  
        Next
      
        Select HostOS
        Case "winnt"
            Local path:=GetEnv( "PATH" )
          
            If ANDROID_PATH path+=";"+ANDROID_PATH+"/tools"
            If ANDROID_PATH path+=";"+ANDROID_PATH+"/platform-tools"
            If JDK_PATH path+=";"+JDK_PATH+"/bin"
            If ANT_PATH path+=";"+ANT_PATH+"/bin"
            If FLEX_PATH path+=";"+FLEX_PATH+"/bin"
          
            If MINGW_PATH path=MINGW_PATH+"/bin;"+path    'override existing mingw path if any...
  
            SetEnv "PATH",path
          
            If JDK_PATH SetEnv "JAVA_HOME",JDK_PATH
  
        Case "macos"

            'Execute "echo $PATH"
            'Print GetEnv( "PATH" )
      
            Local path:=GetEnv( "PATH" )
          
            If JDK_PATH path=JDK_PATH+"/bin:"+path  
            If ANDROID_PATH path+=":"+ANDROID_PATH+"/tools"
            If ANDROID_PATH path+=":"+ANDROID_PATH+"/platform-tools"
            If ANT_PATH path+=":"+ANT_PATH+"/bin"
            If FLEX_PATH path+=":"+FLEX_PATH+"/bin"
          
            SetEnv "PATH",path
          
            'Execute "echo $PATH"
            'Print GetEnv( "PATH" )
            
            If JDK_PATH SetEnv "JAVA_HOME",JDK_PATH
            'Print "macOS JAVA_HOME: " + JDK_PATH

          
        Case "linux"

            Local path:=GetEnv( "PATH" )
          
            If JDK_PATH path=JDK_PATH+"/bin:"+path
            If ANDROID_PATH path=ANDROID_PATH+"/platform-tools:"+path
            If FLEX_PATH path=FLEX_PATH+"/bin:"+path
          
            SetEnv "PATH",path
          
        End
      
    End
  
    Method Execute:Bool( cmd:String,failHard:Bool=True )
    '    Print "Execute: "+cmd
        Local r:=os.Execute( cmd )
        If Not r Return True
        If failHard Die "Error executing '"+cmd+"', return code="+r
        Return False
    End

End